<!DOCTYPE html>
<html lang="en">
	<head>
		<title>Homework3B</title>
		<meta charset="utf-8">
		<style>
			body {
			  	margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
<body>

<div id="container"></div>

<script src="js/three.js"></script>

<script id="VS" type="x-shader/x-vertex">

uniform mat4 projectionMatrix;
uniform mat4 modelViewMatrix;

attribute vec3 position;

void main() {
	
	gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
	
}

</script>

<script id="FS" type="x-shader/x-fragment">

precision mediump float;

/* Function declaration */

// Lighitng

vec3 phongIllumination( vec3 p, vec3 cameraPos );
vec3 lightContribution( vec3 lightPos, vec3 lightColor, float lightIntensity, vec3 p, vec3 cameraPos );

// Normal

vec3 estimateNormal( vec3 p );

// SDF

float sceneSDF( vec3 p );
float sphereSDF( vec3 center, float radius, vec3 p );
float boxSDF( vec3 c, float lx, float ly, float lz, vec3 p );

float intersectSDF( float distA, float distB );
float unionSDF( float distA, float distB );
float differenceSDF( float distA, float distB );

/* Constants */

const float EPSILON = 0.0001;

// Ray marching constants
const int MAX_MARCHING_STEPS = 256;
const float HIT_DIST = 0.01;

// Material constants
const vec3 ka = vec3( 0.2, 0.2, 0.2 ); // Ambient color
const vec3 kd = vec3( 0.2, 0.6, 0.4 ); // Diffuse color
const vec3 ks = vec3( 1.0, 1.0, 1.0 ); // Specular color
const float specularity = 10.0;

/* Uniforms */

uniform vec2 resolution;

void main() {

	float fov = 90.0;
	
	vec3 rayDirection = normalize( vec3( gl_FragCoord.xy - resolution.xy / 2.0, -resolution.y / 2.0 / tan( radians( fov / 2.0 ) ) ) ); // resolution.y / 2.0 -> y, tan( fov / 2.0 ) -> y / z.
	
	// Start ray-marching from the camera's position.
	
	vec3 cameraPos  = vec3( 0.0, 0.0, 5.0 );
	
	vec3 p = cameraPos;
	
	float distToSphere = 0.0;
	
	for ( int i = 1; i <= MAX_MARCHING_STEPS; i++) {
	
		distToSphere = sceneSDF( p );
		
		// Hit
		if ( distToSphere < HIT_DIST ) { break; }
		
		p += rayDirection * distToSphere;
		
	}
	
	// Hit something
	if ( distToSphere < HIT_DIST ) {
	
		vec3 color = phongIllumination( p, cameraPos );
	
		gl_FragColor = vec4( color, 1.0 );
		
	}
	// Hit nothing
	else { gl_FragColor = vec4( 0.0, 0.0, 0.0, 1.0 ); }
	
}

vec3 phongIllumination( vec3 p, vec3 cameraPos ) {

	vec3 color = vec3( 0.0, 0.0, 0.0 );
	
	// Ambient light
	
	vec3 ambientLight_color = vec3( 1.0, 1.0, 1.0 );
	float ambientLight_intensity = 0.1;
	
	vec3 ambientColor = ambientLight_color * ambientLight_intensity;
	color += ambientColor;
	
	// Light 1
	
	vec3 light1_pos = vec3( -3.0, 3.0, -2.0 );
	vec3 light1_color = vec3( 1.0, 1.0, 1.0 );
	float light1_intensity = 0.8;

	color += lightContribution( light1_pos, light1_color, light1_intensity, p, cameraPos );
	
	// Light 2
	
	vec3 light2_pos = vec3( 2.0, 1.0, 3.0 );
	vec3 light2_color = vec3( 1.0, 0.6, 0.2 );
	float light2_intensity = 0.8;
	
	color += lightContribution( light2_pos, light2_color, light2_intensity, p, cameraPos );
	
	return color;

}

vec3 lightContribution( vec3 lightPos, vec3 lightColor, float lightIntensity, vec3 p, vec3 cameraPos ) {
	
	vec3 N = estimateNormal( p );
	
	vec3 L = normalize( lightPos - p );
	
	vec3 R = normalize( reflect( -L, N ) );
	
	vec3 V = normalize( cameraPos - p );
	
	float dotLN = dot( L, N );
	float dotRV = dot( R, V );
	
	vec3 color = vec3( 0.0, 0.0, 0.0 );
	
	// Not lighted
	if ( dotLN <= 0.0 ) { return vec3( 0.0, 0.0, 0.0 ); }
	
	// Reflection not viewable to camera
	if ( dotRV <= 0.0 ) {
		
		color = kd * dotLN * lightIntensity;
		
		return color;
		
	}
	
	color = kd * dotLN * lightIntensity + ks * pow( dotRV, specularity ) * lightIntensity;
	
	return color;

}

vec3 estimateNormal( vec3 p ) {

	float normal_x = sceneSDF( vec3( p.x + EPSILON, p.y, p.z ) ) - sceneSDF( vec3( p.x - EPSILON, p.y, p.z ) );
	float normal_y = sceneSDF( vec3( p.x, p.y + EPSILON, p.z ) ) - sceneSDF( vec3( p.x, p.y - EPSILON, p.z ) );
	float normal_z = sceneSDF( vec3( p.x, p.y, p.z + EPSILON ) ) - sceneSDF( vec3( p.x, p.y, p.z - EPSILON ) );

	return normalize( vec3( normal_x, normal_y, normal_z ) );

}

float sceneSDF( vec3 p ) {
	
	// Define a sphere.
	vec3 sphereCenterPos = vec3( 0.0, -2.0, -3.0 );
	float sphereRadius = 2.0;
	
	// Define a box.
	vec3 boxCenterPos = vec3( 0.0, 3.0, -3.0 );
	float boxLengthX = 5.0;
	float boxLengthY = 1.0;
	float boxLengthZ = 4.0;
	
	float sphereDist = sphereSDF( sphereCenterPos, sphereRadius, p  );
	float boxDist = boxSDF( boxCenterPos, boxLengthX, boxLengthY, boxLengthZ, p );

	float minDist = min( sphereDist, boxDist );
	
	return minDist;
	
}

float sphereSDF( vec3 center, float radius, vec3 p ) { // center: sphere center; p: current point in ray marching;

	return ( length( p - center ) - radius );

}

// c - center, lx - length on X, p - current point on the ray
float boxSDF( vec3 c, float lx, float ly, float lz, vec3 p ) {
	
	if ( ( p.x >= c.x - lx / 2.0 && p.x <= c.x + lx / 2.0 ) && ( p.y >= c.y - ly / 2.0 && p.y <= c.y + ly / 2.0 ) ) { return abs( c.z - p.z ) - lz / 2.0; }
	if ( ( p.x >= c.x - lx / 2.0 && p.x <= c.x + lx / 2.0 ) && ( p.z >= c.z - lz / 2.0 && p.z <= c.z + lz / 2.0 ) ) { return abs( c.y - p.y ) - ly / 2.0; }
	if ( ( p.y >= c.y - ly / 2.0 && p.y <= c.y + ly / 2.0 ) && ( p.z >= c.z - lz / 2.0 && p.z <= c.z + lz / 2.0 ) ) { return abs( c.x - p.x ) - lx / 2.0; }
	
	if ( ( p.x <= c.x - lx / 2.0 || p.x >= c.x + lx / 2.0 ) && ( p.y <= c.y - ly / 2.0 || p.y >= c.y + ly / 2.0 ) && ( p.z >= c.z - lz / 2.0 && p.z <= c.z + lz / 2.0 ) ) {
		return sqrt( pow( abs( c.x - p.x ) - lx / 2.0, 2.0 ) + pow( abs( c.y - p.y ) - ly / 2.0, 2.0 ) );
	}
	if ( ( p.x <= c.x - lx / 2.0 || p.x >= c.x + lx / 2.0 ) && ( p.z <= c.z- lz / 2.0 || p.z >= c.z + lz / 2.0 ) && ( p.y >= c.y - ly / 2.0 && p.y <= c.y + ly / 2.0 ) ) {
		return sqrt( pow( abs( c.x - p.x ) - lx / 2.0, 2.0 ) + pow( abs( c.z - p.z ) - lz / 2.0, 2.0 ) );
	}
	if ( ( p.y <= c.y - ly / 2.0 || p.y >= c.y + ly / 2.0 ) && ( p.z <= c.z- lz / 2.0 || p.z >= c.z + lz / 2.0 ) && ( p.x >= c.x - lx / 2.0 && p.x <= c.x + lx / 2.0 ) ) {
		return sqrt( pow( abs( c.y - p.y ) - ly / 2.0, 2.0 ) + pow( abs( c.z - p.z ) - lz / 2.0, 2.0 ) );
	}
	
	return sqrt( pow( abs( c.x - p.x ) - lx / 2.0, 2.0 ) + pow( abs( c.y - p.y ) - ly / 2.0, 2.0 ) + pow( abs( c.z - p.z ) - lz / 2.0, 2.0 ) );
	
}

float intersectSDF( float distA, float distB ) {

	return max( distA, distB );

}

float unionSDF( float distA, float distB ) {

	return min( distA, distB );

}

float differenceSDF( float distA, float distB ) {

	return max( distA, -distB );

}

</script>

<script>

// Essentials

var container;

var camera, scene, renderer;

var VS = document.getElementById( "VS" ).textContent;
var FS = document.getElementById( "FS" ).textContent;

init();
animate();

function init() {

	container = document.getElementById( "container" );
	
	// Camera
	
	camera = new THREE.OrthographicCamera( window.innerWidth / -2, window.innerWidth / 2, window.innerHeight / 2, window.innerHeight / -2, 0.1, 1000 );
	
	camera.position.z  = 10;
	
	// Scene
	scene = new THREE.Scene();
	
	// Renderer
	
	renderer = new THREE.WebGLRenderer();
	renderer.setClearColor( 0x003366 );
	renderer.setSize( window.innerWidth, window.innerHeight );
	container.appendChild( renderer.domElement );
	
	addPlane();
	
}

function addPlane() {

	// Geometry
	
	var planeGeometry = new THREE.PlaneGeometry( window.innerWidth, window.innerHeight, 1, 1 );
	
	// Material
	
	var planeMaterial = new THREE.RawShaderMaterial( {
	
		uniforms: {
		
			resolution: { type: "v2", value: new THREE.Vector2( window.innerWidth, window.innerHeight ) }
		
		},
		
		vertexShader: VS,
		fragmentShader: FS
	
	} );
	
	// Mesh
	
	var planeMesh = new THREE.Mesh( planeGeometry, planeMaterial );
	scene.add( planeMesh );

}

function animate() {

	requestAnimationFrame( animate );
	
	render();

}

function render() {

	renderer.render( scene, camera );

}

</script>

</body>
</html>
